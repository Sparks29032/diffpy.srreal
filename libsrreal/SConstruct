# Customizable compile variables
vars = Variables('sconsvars.py')
vars.Add('Tests', 'Custom list of unit test sources', '')


# Default build environment
env = DefaultEnvironment(variables=vars)
Help(vars.GenerateHelpText(env))

# Library target
src_lib = Split('''
    PairQuantity.cpp
    ''')

libsrreal = SharedLibrary('libsrreal', src_lib)
lib = Alias('lib', libsrreal)

# Environment for building unit test driver
env_test = env.Clone()
env_test.ParseConfig('pkg-config --cflags --libs cppunit')
env_test.Append(LIBS='libsrreal', LIBPATH='.')
env_test.AppendENVPath('LD_LIBRARY_PATH', '.')

# alltests - the unit test driver
src_tests = Split(env_test['Tests']) or Glob('Test*.cpp')
alltests = env_test.Program('alltests', ['alltests.cpp'] + src_tests)

# alias for executing unit test modules
test = env_test.Alias('test', [alltests], alltests[0].abspath)
# test = env_test.Alias('test', [alltests], 'env')
AlwaysBuild(test)

Default(alltests)

# vim: ft=python
