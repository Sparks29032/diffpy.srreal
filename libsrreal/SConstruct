Environment().EnsureSConsVersion(0, 98)
import os.path

# Customizable compile variables
vars = Variables('sconsvars.py')

vars.Add('tests', 'Custom list of unit test sources', 'all')
vars.Add(EnumVariable('build', 'compiler settings', 'debug',
                      allowed_values=('debug', 'fast')))
vars.Add(PathVariable('prefix',
    'installation prefix directory', '/usr/local'))
vars.Add(PathVariable('libdir',
    'object code library directory [prefix/lib]', '',
    validator=PathVariable.PathAccept))
vars.Add(PathVariable('includedir',
    'installation directory for C++ header files [prefix/include]', '',
    validator=PathVariable.PathAccept))

# Default build environment
env = DefaultEnvironment(variables=vars)
Help(vars.GenerateHelpText(env))

if env['build'] == 'debug':
   env.Append(CCFLAGS='-g')
env.Append(CCFLAGS='-Wall')
env.PrependUnique(CPPPATH='#/include')

Export('env')

# Execute subsidiary sconscripts

# shared library object
lib = SConscript('src/SConscript')
# unit tests
SConscript('tests/SConscript')

# Installation targets

# library object
prefix = env['prefix']
libdir = env['libdir'] or \
        Dir(os.path.join(prefix, 'lib'))
Alias('install-lib', Install(libdir, lib))

# includes
includedir = env['includedir'] or \
        Dir(os.path.join(env['prefix'], 'include/diffpy/srreal'))
include_sources = Glob('include/diffpy/srreal/*.hpp')
Alias('install-include', Install(includedir, include_sources))

# everything
Alias('install', ['install-include', 'install-lib'])

# vim: ft=python
